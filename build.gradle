subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        implementation 'javax:javaee-web-api:7.0'
    }

    dependencies {
        implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.3' // Use a recent stable version
        runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:2.3.3' // Use a recent stable version
        implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    }
}

project(':acmeair-common') {
}

project(':acmeair-services') {
    dependencies {
        implementation project(':acmeair-common')
        implementation 'com.googlecode.json-simple:json-simple:1.1.1'

    }
}

project(':acmeair-services-morphia') {
    dependencies {
        implementation project(':acmeair-common')
        implementation project(':acmeair-services')
        implementation 'org.mongodb:mongo-java-driver:2.13.0'
        implementation 'org.mongodb.morphia:morphia:0.110'
    }
}


project(':acmeair-loader') {
    dependencies {
        implementation project(':acmeair-common')
        implementation project(':acmeair-services')
    }
}


project(':acmeair-webapp') {
    apply plugin: 'war'

    dependencies {
        implementation project(':acmeair-common')
        implementation project(':acmeair-services')
        implementation project(':acmeair-loader')

        def includeService = "ALL"
        if (project.hasProperty('service') ) {
           println "property service exists and has a value of $service "
           includeService = service
        } else {
           println "property service was not set. Using the default value of $includeService"
           println "   To pick a specific service set the service property using the -P parameter, for example gradlew -Pservice=wxs build " 
        }

        if ("$includeService".equalsIgnoreCase("morphia") || "$includeService".equalsIgnoreCase("ALL")) {
           println "Including mongoDB morphia service "
           implementation (project(':acmeair-services-morphia')) {
              transitive = false
           }
 
           implementation ('org.mongodb.morphia:morphia:0.110')  {
              transitive = false
           }
        }

        providedRuntime 'javax:javaee-web-api:7.0'
    }
}

